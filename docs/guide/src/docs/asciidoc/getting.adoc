== Getting started

The following example presents a **Document** aggregate, the Document can support different types of events such as **creating a document** (Created)
or **appending text** to the document (Appended).

Also an **EventStore** implementation is created to store the aggregate events. This particular implementation stores events
and snapshots **as csv files**.

[source, groovy]
.save an aggregate
----
@Grab('io.grooviter:memento-base:0.1.0')
import io.grooviter.memento.*

// EVENTS....
class Created extends Event {
   String title, author
}

class Appended extends Event {
   String content
}

// AGGREGATE...
class Document extends Aggregate {
   String title, author, content

   static Document create(String title, String author) {
       Document document = new Document(id: UUID.randomUUID())
       return document.apply(new Created(title: title, author: author, version: nextVersion))
   }

   Document append(String content) {
       return this.apply(new Appended(content: "${this.content} $content", version: nextVersion))
   }

   Document apply(Created created) {
       super.apply(created)
       this.title = created.title
       this.author = created.author
       return this
   }

   Document apply(Appended appended) {
       super.apply(appended)
       this.content += appended.content
       return this
   }
}

// EVENTSTORE IMPLEMENTATION...
EventStore eventStore = Memento.builder()
    .csvEventStorage('/tmp/events.csv', '/tmp/snapshots.csv')
    .snapshotThreshold(2) // snapshot every 2 events
    .build()

// CREATING AND STORING AN AGGREGATE....
Document document = Document.create("Memento", "Christopher Nolan")
    .append("A man who, as a result of an injury,")
    .append(", has anterograde amnesia")
    .append("and has short-term memory loss")
    .append("approximately every fifteen minutes.")

// SAVING THE AGGREGATE's EVENTS...
eventStore.save(document)
----

If you take a look at the **events.csv** file you will see the following.

[source, shell]
.events.csv
----
...-6740974752dc|1|DocumentCreated|{"title":"Memento","aggregateId":"5f5469ba-a031-4ccb...
...-6740974752dc|2|Appended|{"content":"A man who, as a result of an injury","aggregate...
...
----