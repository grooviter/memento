plugins {
    id 'org.kordamp.gradle.groovy-project'
    id 'org.kordamp.gradle.codenarc'
    id 'org.jreleaser' version '1.3.1'
}

ext {
    mavenUsername   = findProperty("PUBLISH_REPO_USERNAME")
    mavenPassword   = findProperty("PUBLISH_REPO_PASSWORD")
    shouldRelease   = !"$version".endsWith('SNAPSHOT')
}

config {
    release = shouldRelease

    info {
        name          = 'Memento'
        vendor        = 'Grooviter'
        description   = 'Memento tries to provide a high level abstraction for different EventStore implementations'
        inceptionYear = '2021'
        tags          = ['event-sourcing', 'event-store']

        links {
            website      = 'https://github.com/grooviter/memento'
            issueTracker = 'https://github.com/grooviter/memento/issues'
            scm          = 'https://github.com/grooviter/memento.git'
        }
        people {
            person {
                id    = 'marioggar'
                name  = 'Mario Garcia'
                roles = ['developer']
            }
        }
        repositories {
            repository {
                name = 'releases'
                url  = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username = mavenUsername
                    password = mavenPassword
                }
            }
            repository {
                name = 'snapshots'
                url  = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                credentials {
                    username = mavenUsername
                    password = mavenPassword
                }
            }
        }
    }
    artifacts {
        minpom {
            enabled
        }
    }
    publishing {
        releasesRepository  = 'releases'
        snapshotsRepository = 'snapshots'
        signing {
            enabled    = shouldRelease
            keyId      = findProperty("PUBLISH_SIGN_KEY_ID")
            secretKey  = findProperty("PUBLISH_SIGN_KEY")
            password   = findProperty("PUBLISH_SIGN_SECRET")
        }
        pom {
            overwriteInceptionYear = "2021"
        }
    }
    docs {
        guide {
            publish {
                enabled = true
                message = "chores(docs): publish docs for version ${version}"
            }
        }
        groovydoc {
            enabled = true
            replaceJavadoc = true
            options {
                docTitle = "Memento"
                windowTitle = "Memento: Groovy EventStore Library"
                link 'https://docs.oracle.com/javase/11/docs/api/', 'java'
                link 'http://docs.groovy-lang.org/latest/html/api/', 'groovy', 'org.codehaus.groovy'
            }
            aggregate {
                enabled        = true
                replaceJavadoc = true
            }
        }
    }
    licensing {
        enabled = false
        includes = ['**/*.groovy', '**/*.gradle']
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }
    quality {
        codenarc {
            enabled                = false
            toolVersion            = '2.0.0'
            ignoreFailures         = true
            maxPriority1Violations = 0
            maxPriority2Violations = Integer.MAX_VALUE
            maxPriority3Violations = Integer.MAX_VALUE
            aggregate { }
        }
    }
    coverage {
        jacoco {
            toolVersion = '0.8.6'
        }
    }
}

jreleaser {
    release {
        github {
            token     = findProperty('PUBLISH_GH_TOKEN')
            overwrite = true
            changelog {
                formatted = 'ALWAYS'
                preset    = 'conventional-commits'
                hide {
                    uncategorized = true
                }
            }
            prerelease {
                enabled = !shouldRelease
            }
        }
    }
    signing {
        active = 'NEVER'
    }
    deploy {
        maven { active = 'NEVER' }
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    tasks.withType(PublishToMavenRepository).configureEach {
        enabled = it.project.projectDir.parentFile.name != 'samples'
    }

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = findProperty('sourceCompatibility')
        targetCompatibility = findProperty('targetCompatibility')
        options.encoding    = 'UTF-8'
    }

    configurations.codenarc {
        resolutionStrategy {
            force "org.codehaus.groovy:groovy:$groovy"
        }
    }
}