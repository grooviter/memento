plugins {
    id 'org.kordamp.gradle.groovy-project' version '0.45.0'
    id 'org.kordamp.gradle.jacoco' version '0.45.0'
    id 'org.kordamp.gradle.codenarc' version '0.45.0'
}

config {
    release = (rootProject.findProperty('release') ?: false) as Boolean

    info {
        name        = 'Memento'
        vendor      = 'Grooviter'
        description = 'Poor man\'s event-store abstraction'

        links {
            website      = 'https://github.com/grooviter/memento'
            issueTracker = 'https://github.com/grooviter/memento/issues'
            scm          = 'https://github.com/grooviter/memento.git'
        }

        people {
            person {
                id    = 'marioggar'
                name  = 'Mario Garcia'
                roles = ['developer']
            }
        }

        repositories {
            repository {
                name = 'mavenReleases'
                url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            }
            repository {
                name = 'mavenSnapshots'
                url  = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            }
        }
    }

    licensing {
        enabled = false
        includes = ['**/*.groovy', '**/*.gradle']
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    coverage {
        jacoco {
            toolVersion = '0.8.6'
        }
    }

    quality {
        codenarc {
            enabled        = false
            toolVersion    = '2.0.0'
            ignoreFailures = true
            maxPriority1Violations = 0
            maxPriority2Violations = Integer.MAX_VALUE
            maxPriority3Violations = Integer.MAX_VALUE
            aggregate { }
        }
    }

    docs {
        guide {
            publish {
                message = "chores(docs): publish docs for version ${version}"
            }
        }
        groovydoc {
            enabled = true
            replaceJavadoc = true
        }
    }

    publishing {
        enabled             = true
        releasesRepository  = 'mavenReleases'
        snapshotsRepository = 'mavenSnapshots'
        signing {
            enabled    = !version.endsWith("-SNAPSHOT")
            keyId      = findProperty("signingKeyId")
            secretKey  = findProperty("signingKey")
            password   = findProperty("signingPassword")
        }
    }
}

projects.subprojects {
    dir('samples') {
        config {
            publishing {
                enabled = false
            }
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = 8
        targetCompatibility = 8
        options.encoding    = 'UTF-8'
    }

    configurations.codenarc {
        resolutionStrategy {
            force "org.codehaus.groovy:groovy:$groovy"
        }
    }
}
